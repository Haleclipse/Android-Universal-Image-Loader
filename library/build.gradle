apply plugin: 'com.android.library'

android {
    compileSdkVersion rootProject.ext.CompileSdk
    buildToolsVersion rootProject.ext.BuildTools

    defaultConfig {
        minSdkVersion rootProject.ext.MinSdk
        targetSdkVersion rootProject.ext.TargetSdk
        versionCode 40
        versionName "1.9.5"
        vectorDrawables.useSupportLibrary = true
    }
}

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.3.2'
    testCompile 'com.squareup.assertj:assertj-android:1.1.1'
    
    compile "com.android.support:appcompat-v7:$rootProject.ext.SupportLibrary"
}


// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
artifacts {
    archives sourcesJar
}

// Build a jar file in addition to the default aar file
android.libraryVariants.all { variant ->
    def name = variant.buildType.name
    def task = project.tasks.create "jar${name.capitalize()}", Jar
    task.dependsOn variant.javaCompile
    task.from variant.javaCompile.destinationDir
    artifacts.add('archives', task);
}

apply from: '../gradle/maven_push.gradle'
